PREFIX sport: <https://sportschema.org/ontologies/main/>
PREFIX bvo: <https://github.com/andreazammarchi3/BeachVolleyballOntology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Query to identify the player who scored the highest number of points in the competition World Tour 2024 Cancun

SELECT ?athlete ?competition (SUM(?points) AS ?totalPoints)
WHERE {
    ?athlete a sport:Athlete .

    ?competition rdfs:label "World Tour 2024 Cancun"@en .

    ?match sport:matchInCompetition ?competition .

    ?performance a bvo:AthletePerfomance .
    ?performance bvo:performanceOf ?athlete .
    ?performance bvo:performanceIn ?match .
    ?performance bvo:points ?points .
}
GROUP BY ?athlete ?competition
ORDER BY DESC(?totalPoints)
LIMIT 1
